import static java.nio.charset.StandardCharsets.UTF_8

plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '1.7.2'
    id "org.springframework.boot" version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

group 'ue.app.ntpartner'
version grgit.describe()

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    runtimeOnly('mysql:mysql-connector-java')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    testImplementation('com.h2database:h2')
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.encoding = UTF_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.encoding = UTF_8
}

wrapper {
    gradleVersion = '4.10.2'
    distributionType = Wrapper.DistributionType.ALL
}

task createArtifactDocker(type: Copy) {
    group 'dist'
    dependsOn assemble
    from('artifact/docker') {
        include '**'
    }
    from('build/libs') {
        include '*.jar'
    }
    from('src/main/resources') {
        include 'logback.xml'
    }
    destinationDir file('build/artifact/docker')
}